plugins {
    id 'com.android.application'
    id 'com.google.protobuf' version '0.9.1'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def jksKeyPassword = System.getenv('ANDROID_JKS_KEY_PASSWORD')
if (jksKeyPassword == null) {
    jksKeyPassword = localProperties.getProperty('jks.key')
    println('没有设置JKS钥密码的环境变量, 从配置文件中读取')
}

def jksStorePassword = System.getenv('ANDROID_JKS_STORE_PASSWORD')
if (jksStorePassword == null) {
    jksStorePassword = localProperties.getProperty('jks.store')
    println('没有设置JKS库密码的环境变量, 从配置文件中读取')
}

android {
    compileSdk 33

    namespace 'red.lilu.app'

    defaultConfig {
        applicationId "red.lilu.app.polong"
        minSdk 24
        targetSdk 33
        versionCode 100
        versionName "1.0.0"
    }

    signingConfigs {
        lilu {
            keyAlias 'lilu.red'
            keyPassword "$jksKeyPassword"
            storeFile file('lilu.red.jks')
            storePassword "$jksStorePassword"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.lilu
        }
        debug {
            signingConfig signingConfigs.lilu
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'io.grpc:grpc-protobuf-lite:1.54.1'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    implementation "androidx.datastore:datastore:1.0.0"
    implementation "androidx.datastore:datastore-rxjava3:1.0.0"

    // CameraX core library using the camera2 implementation
    def camera_version = "1.1.0"
    implementation "androidx.camera:camera-core:${camera_version}"
    implementation "androidx.camera:camera-camera2:${camera_version}"
    implementation "androidx.camera:camera-lifecycle:${camera_version}"
    implementation "androidx.camera:camera-view:1.1.0"

    implementation 'com.google.android.exoplayer:exoplayer-core:2.17.1'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.17.1'

    implementation 'com.google.guava:guava:30.1-android'
    implementation 'joda-time:joda-time:2.10.10'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'

    implementation 'com.github.bumptech.glide:glide:4.15.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'

    implementation project(":gomobile")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.54.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}
